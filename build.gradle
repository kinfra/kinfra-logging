plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'ru.kontur.jinfra.presets' version '1.1'
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}

ext {
    versions = [
            slf4j: '1.7.28',
            logback: '1.2.3',
            coroutines: '1.3.1',
    ]

    libs = [
        slf4jApi: "org.slf4j:slf4j-api:${versions.slf4j}",
        coroutinesCore: "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}",
        logbackClassic: "ch.qos.logback:logback-classic:${versions.logback}",
    ]
}

group = "ru.kontur.jinfra"
version = "0.14"

dependencies {
    implementation libs.slf4jApi
    implementation libs.coroutinesCore

    testRuntimeOnly libs.logbackClassic
}

jar {
    manifest.attributes "Automatic-Module-Name": "ru.kontur.jinfra.logging"
}

publishing {
    repositories {
        def properties = project.properties
        def repoUrl = properties["publishing.repo.url"]
        if (repoUrl) {
            maven {
                url = repoUrl

                credentials {
                    username = properties["publishing.repo.username"]
                    password = properties["publishing.repo.password"]
                }
            }
        }
    }
}
