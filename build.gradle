plugins {
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'ru.kontur.kinfra.presets' version '1.5'
    id 'me.champeau.gradle.jmh' version '0.5.3'
}

ext {
    versions = [
            slf4j: '1.7.28',
            logback: '1.2.3',
            coroutines: '1.4.3',
    ]

    libs = [
        slf4jApi: "org.slf4j:slf4j-api:${versions.slf4j}",
        coroutinesCore: "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}",
        coroutinesSlf4j: "org.jetbrains.kotlinx:kotlinx-coroutines-slf4j:${versions.coroutines}",
        logbackClassic: "ch.qos.logback:logback-classic:${versions.logback}",
    ]
}

group = "ru.kontur.kinfra"
version = "0.3"

dependencies {
    implementation libs.slf4jApi
    implementation libs.coroutinesCore

    testRuntimeOnly libs.logbackClassic

    jmh "org.openjdk.jmh:jmh-core:1.25"
    jmh "org.openjdk.jmh:jmh-generator-annprocess:1.25"
    jmh libs.coroutinesSlf4j
    jmh libs.logbackClassic

    constraints {
        runtimeOnly "ru.kontur.jinfra:jinfra-logging:0.15"
    }
}

kotlin {
    explicitApi()
}

tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile) {
    kotlinOptions.freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
}

jar {
    manifest.attributes "Automatic-Module-Name": "ru.kontur.kinfra.logging"
}

publishing {
    repositories {
        def properties = project.properties
        def repoUrl = properties["publishing.repo.url"]
        if (repoUrl) {
            maven {
                url = repoUrl

                credentials {
                    username = properties["publishing.repo.username"]
                    password = properties["publishing.repo.password"]
                }
            }
        }
    }
}

jmh {
    include = ["benchmarks.SimpleLogging.*"]
    benchmarkMode = ["avgt"]
    batchSize = 100_000
    iterations = 10
    timeOnIteration = '1s'
    warmupBatchSize = 100_000
    warmupIterations = 5
    warmup = "1s"

    timeUnit = 'ms'
    fork = 1
    forceGC = true

    // don't include logback.xml from tests
    includeTests = false
}
